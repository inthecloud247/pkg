# Maintainer: Thomas Jost <schnouki@schnouki.net>
pkgname=libspotify
pkgver=12.1.51
pkgrel=1
pkgdesc="C API package allowing third-party developers to write applications that utilize the Spotify music streaming service"
arch=(i686 x86_64)
url="https://developer.spotify.com/technologies/libspotify/"
license=('custom')
depends=('glibc')
source=(http://developer.spotify.com/download/libspotify/libspotify-${pkgver}-Linux-${CARCH}-release.tar.gz)

md5sums=('04735b890da0b1fc7f1f14e68a5293de')
sha256sums=('941ab4ba10bcd6ec4e96127afd095a39e11bc955de0882734c97e4f588b155ae')
if [ "$CARCH" == "x86_64" ]; then
  md5sums=('83efddcc195d6ff12b24c97c767a5e45')
  sha256sums=('43a14e0732ba6ae30078fac105d0e2998d04d5f5c396a4968386bc4e22491058')
fi

build() {
  cd "$srcdir/$pkgname-$pkgver-Linux-$CARCH-release"

  # Don't do stupid things from a Makefile
  msg2 "Patching Makefile..."
  sed -i 's/ldconfig//' Makefile
}

package() {
  cd "$srcdir/$pkgname-$pkgver-Linux-$CARCH-release"

  make prefix="$pkgdir/usr" install

  # Install documentation
  cp -R share "$pkgdir"/usr/share
  mkdir -p "$pkgdir"/usr/share/man
  mv "$pkgdir"/usr/share/man3 "$pkgdir"/usr/share/man/man3

  # Correct pkgconfig file
  sed -e s:PKG_PREFIX:/usr:g \
          < lib/pkgconfig/libspotify.pc \
          > "$pkgdir"/usr/lib/pkgconfig/libspotify.pc

  # Custom license
  install -Dm644 LICENSE licenses.xhtml "$pkgdir"/usr/share/doc/libspotify
  mkdir -p "$pkgdir"/usr/share/licenses/libspotify
  ln -s ../../doc/libspotify/LICENSE "$pkgdir"/usr/share/licenses/libspotify/LICENSE
}

# Local Variables:
# pkgbuild-update-sums-on-save: nil
# End:
