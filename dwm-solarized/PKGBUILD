# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Dag Odenhall <dag.odenhall@gmail.com>
# Contributor: Grigorios Bouzakis <grbzks@gmail.com>

_realname=dwm
pkgname=${_realname}-solarized
pkgver=6.0
pkgrel=1
pkgdesc="A dynamic window manager for X with Solarized colors"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama')
conflicts=('dwm')
source=(http://dl.suckless.org/dwm/dwm-$pkgver.tar.gz
       config.h)
md5sums=('8bb00d4142259beb11e13473b81c0857'
         'cde44d943eaeb6b4b5c65daf0ce0d823')

build() {
  cd $srcdir/${_realname}-$pkgver
  cp $srcdir/config.h config.h
  sed -i 's/CPPFLAGS =/CPPFLAGS +=/g' config.mk
  sed -i 's/^CFLAGS = -g/#CFLAGS += -g/g' config.mk
  sed -i 's/^#CFLAGS = -std/CFLAGS += -std/g' config.mk
  sed -i 's/^LDFLAGS = -g/#LDFLAGS += -g/g' config.mk
  sed -i 's/^#LDFLAGS = -s/LDFLAGS += -s/g' config.mk
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $srcdir/${_realname}-$pkgver
  make PREFIX=/usr DESTDIR=$pkgdir install
  install -m644 -D LICENSE $pkgdir/usr/share/licenses/${_realname}/LICENSE
  install -m644 -D README $pkgdir/usr/share/doc/${_realname}/README
}
